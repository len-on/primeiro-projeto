extends CharacterBody2D

enum PlayerState {
	idle,
	walk,
	jump,
	fall,
	duck,
	slide,
	wall,
	swimming,
	hurt
}

@onready var anim: AnimatedSprite2D = $AnimatedSprite2D
@onready var collision_shape: CollisionShape2D = $CollisionShape2D
@onready var reload_timer: Timer = $ReloadTimer
@onready var hitbox_collision_shape: CollisionShape2D = $Hitbox/CollisionShape2D
@onready var left_wall_detector: RayCast2D = $LeftWallDetector
@onready var right_wall_detector: RayCast2D = $RightWallDetector

@export var max_speed = 180.0
@export var acceleration = 400
@export var deceleration = 400
@export var slide_deceleration = 100
@export var wall_acceleration = 40
@export var wall_jump_velocity = 240
@export var water_max_speed = 100
@export var water_acceleration = 200


const JUMP_VELOCITY = -300.0

var jump_count = 0
@export var max_jump_count = 2
var direction = 0
var status: PlayerState



func _ready() -> void:
	go_to_idle_state()

func _physics_process(delta: float) -> void:
	
	match status:
		PlayerState.idle:
			idle_state(delta)
		PlayerState.walk:
			walk_state(delta)
		PlayerState.jump:
			jump_state(delta)
		PlayerState.fall:
			fall_state(delta)
		PlayerState.duck:
			duck_state(delta)
		PlayerState.slide:
			slide_state(delta)
		PlayerState.wall:
			wall_state(delta)
		PlayerState.swimming:
			swimming_state(delta)
		PlayerState.hurt:
			hurt_state(delta)
		
			
			
	move_and_slide()
			
			
func go_to_idle_state():
	status = PlayerState.idle
	anim.play("idle")
	
func go_to_walk_state():
	status = PlayerState.walk
	anim.play("walk")

func go_to_jump_state():
	status = PlayerState.jump
	anim.play("jump")
	velocity.y	= JUMP_VELOCITY
	jump_count += 1
	
func go_to_fall_state():
	status = PlayerState.fall
	anim.play("fall")
	
func go_to_duck_state():
	status = PlayerState.duck
	anim.play("duck")
	set_small_collide()
	
func exit_from_duck_state():
	set_large_collider()
	
func go_to_slide_state():
	status = PlayerState.slide
	anim.play("slide")
	set_small_collide()
	
	
func exit_from_slide_state():
	set_large_collider()
	
func go_to_wall_state():
	
	status = PlayerState.wall
	anim.play("wall")
	
	velocity = Vector2.ZERO
	
	jump_count = 0
	
func got_to_swimming_state():
	status = PlayerState.swimming
	anim.play("swimming")
	velocity.y = min(velocity.y, 150)

func go_to_hurt_state():
	if status == PlayerState.hurt:
		return
	
	status = PlayerState.hurt
	anim.play("hurt")
	velocity.x = 0
	reload_timer.start()
	

func idle_state(delta):
	apply_gravity(delta)
	
	move(delta)
	if velocity.x != 0:
		go_to_walk_state()
		return
		
	if Input.is_action_just_pressed("ui_jump"):
		go_to_jump_state()
		return
		
	if Input.is_action_pressed("ui_down"):
		go_to_duck_state()
		return
	
func walk_state(delta):
	apply_gravity(delta)
	
	move(delta)
	if velocity.x == 0:
		go_to_idle_state()
		return
		
	if Input.is_action_just_pressed("ui_jump"):
		go_to_jump_state()
		return
		
	if Input.is_action_just_pressed("ui_down"):
		go_to_slide_state()
		return
		
	if !is_on_floor():
		jump_count += 1
		go_to_fall_state()
		return
	
func jump_state(delta):
	apply_gravity(delta)
	
	move(delta)
	
	if Input.is_action_just_pressed("ui_jump") && can_jump():
		go_to_jump_state()
		return
		
	if velocity.y > 0:
		go_to_fall_state()
		return
	
func fall_state(delta):
	apply_gravity(delta)
	
	move(delta)
	
	if Input.is_action_just_pressed("ui_jump") && can_jump():
		go_to_jump_state()
		return
		
	
	if is_on_floor():
		jump_count = 0
		if velocity.x == 0:
			go_to_idle_state()
		else :
			go_to_walk_state()
		return
		
	if (left_wall_detector.is_colliding() or right_wall_detector.is_colliding()) && is_on_wall():
		go_to_wall_state()
		return

func duck_state(delta):
	apply_gravity(delta)
	
	update_direction()
	if Input.is_action_just_released("ui_down"):
		exit_from_duck_state()
		go_to_idle_state()
		return
		
func slide_state(delta):
	apply_gravity(delta)
	
	velocity.x = move_toward(velocity.x, 0, slide_deceleration * delta)
	
	if Input.is_action_just_released("ui_down"):
		exit_from_slide_state()
		go_to_walk_state()
		return
		
	if velocity.x == 0:
		exit_from_slide_state()
		go_to_duck_state()
		return
		
func wall_state(delta):
	
	velocity.y += wall_acceleration * delta
	
	if left_wall_detector.is_colliding():
		anim.flip_h = false
		direction = 1
		
	elif right_wall_detector.is_colliding():
		anim.flip_h = true
		direction = -1
	else:
		go_to_fall_state()
		return
	
	if is_on_floor():
		go_to_idle_state()
		return
		
	if Input.is_action_just_pressed("ui_jump"):
		velocity.x = wall_jump_velocity * direction
		go_to_jump_state()
		return
	
func swimming_state(delta):
	update_direction()
	
	if direction:
		velocity.x = move_toward(velocity.x, water_max_speed * direction, water_acceleration * delta)
	else:
		velocity.x = move_toward(velocity.x, 0, water_acceleration * delta)
		
	var vertical_direction = Input.get_axis("ui_jump", "ui_down")
	
	if vertical_direction:
		velocity.y = move_toward(velocity.y, water_max_speed * vertical_direction, water_acceleration * delta)
	else:
		velocity.y	= move_toward(velocity.y, 0, water_acceleration * delta)
func hurt_state(delta):
	apply_gravity(delta)
	pass

func move(delta):
	update_direction()
	
	if direction:
		velocity.x = move_toward(velocity.x, direction * max_speed, acceleration * delta)
	else:
		velocity.x = move_toward(velocity.x, 0, deceleration * delta)

func apply_gravity(delta):
	# Add the gravity.
	if not is_on_floor():
		velocity += get_gravity() * delta
		
func update_direction():
	direction = Input.get_axis("ui_left", "ui_right")
	
	if direction < 0:
		anim.flip_h = true
	elif direction > 0:
		anim.flip_h = false
		
func can_jump() -> bool:
	return jump_count < max_jump_count 
	
func set_small_collide():
	collision_shape.shape.radius = 5
	collision_shape.shape.height = 11
	collision_shape.position.y = 2.5
	
	hitbox_collision_shape.shape.size.y = 10
	hitbox_collision_shape.position.y = 3
	
func set_large_collider():
	collision_shape.shape.radius = 6
	collision_shape.shape.height = 15
	collision_shape.position.y = 0.5
	
	hitbox_collision_shape.shape.size.y = 15
	hitbox_collision_shape.position.y = 0.5


func _on_hitbox_area_entered(area: Area2D) -> void:
	if area.is_in_group("Enemies"):
		hit_enemy(area)
	elif area.is_in_group("LethalArea"):
		hit_lethal_area()
		
func _on_hitbox_body_entered(body: Node2D) -> void:
	if body.is_in_group("LethalArea"):
		go_to_hurt_state()
	elif body.is_in_group("Water"):
		got_to_swimming_state()
			
func hit_enemy(area: Area2D):
	if velocity.y > 0:
		#inimigo morre
		area.get_parent().take_damege()
		go_to_jump_state()
	else:
		#player morre
		go_to_hurt_state()
	
func hit_lethal_area():
	go_to_hurt_state()


func _on_reload_timer_timeout() -> void:
	get_tree().reload_current_scene() # Replace with function body.


func _on_hitbox_body_exited(body: Node2D) -> void:
	if body.is_in_group("Water"):
		jump_count = 0
		go_to_jump_state()
		
